template("cp_static_library") {
  static_library(target_name) {
    forward_variables_from(invoker, "*")
    if (!defined(invoker.configs)) {
      configs = []
    }
    configs += [ "//:cp_config" ]
  }
}

template("cp_shared_library") {
  shared_library(target_name) {
    forward_variables_from(invoker, "*")
    if (!defined(invoker.configs)) {
      configs = []
    }
    configs += [ "//:cp_config" ]
  }
}

template("cp_executable") {
  executable(target_name) {
    forward_variables_from(invoker, "*")
    if (!defined(invoker.configs)) {
      configs = []
    }
    configs += [ "//:cp_config" ]
  }
}

template("cp_test") {
  executable(target_name) {
    forward_variables_from(invoker, "*")
    testonly = true
    if (!defined(invoker.configs)) {
      configs = []
    }
    configs += [
      "//:cp_config",
      "//:cp_test_config",
    ]
    if (!defined(invoker.deps)) {
      deps = []
    }
    deps += [ "//third_party/gtest:gtest" ]
  }
}
